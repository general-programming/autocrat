buildscript {
    ext.kotlin_version = '1.3.61'
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "3.+", changing: true
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: 'kotlin'

version = modVersion
group = modGroup
archivesBaseName = modBaseName

compileJava {
    options.compilerArgs << '-parameters'
}

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

minecraft {
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'snapshot', version: '20190719-1.14.3'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    runs {
        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                autocrat {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'autocrat', '--all', '--output', file('src/generated/resources/')

            mods {
                autocrat {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven {
        url "http://maven.shadowfacts.net/"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.14.4-28.2.0"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "net.shadowfacts:Forgelin:1.7.3"
    embed "io.github.hedgehog1029:frame:1.2.0-PRE"
    compile "silly511.backups:Backups:1.12.2-1.3"
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }

    manifest {
        attributes([
            "Specification-Title": "autocrat",
            "Specification-Vendor": "General Programming",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"General Programming",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
